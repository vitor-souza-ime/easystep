;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*														*
;*	 		            EASYSTEP						*
;*	       	   CERNE TECNOLOGIA E TREINAMENTO			*
;*   		  VERSÃO 1.0	   DATA:29/04/2005			*
;*														*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*	 			 DESCRIÇÃO DO ARQUIVO FONTE				*	
;*														*
;* easy_step_00 - versão inicial executando até o comando 25 *
;* easy_step_01 - comando 26 até 31						*
;* easy_step_02 - comando 32 até ...					*
;* easy_step_03 - comando 32 até ...					*
;* easy_step_04 - comando 32 até ...					*
;*														*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
 ERRORLEVEL 2
 #INCLUDE <P16F648A.INC> 	;ARQUIVO PADRÃO PARA PIC16F648A
 __CONFIG _CP_ON & _INTRC_OSC_NOCLKOUT & _PWRTE_ON & _MCLRE_ON & _LVP_OFF & _WDT_ON & _BODEN_OFF & _DATA_CP_ON  
 	
 	;NESTE PONTO É INFORMADO A CONFIGURAÇÃO DA MÁQUINA, OU SEJA, SE TERÁ
 	;CÓDIGO DE PROTEÇÃO OU O TIPO DE CRISTAL QUE SERÁ UTILIZADO	

 	;CONFIGURAÇÃO DA MÁQUINA
 	; CODE PROTECT		        -> OFF
 	; OSCILADOR    		        -> CRISTAL OU RESSONADOR
 	; POWER-UP     		        -> DESLIGADO
 	; MCLR EXTERNO 		        -> LIGADO		
 	; PROGRAMAÇÃO EM BAIXA VOLTAGEM -> DESLIGADO
	; WATCH DOG			-> DESLIGADO
	; BROWN-OUT RESET		-> DESLIGADO
	; PROTEÇÃO DE EEPROM		-> DESLIGADO
	 	
;*	*	*	*	*	*	*	*	*	*
;*			    PAGINAÇÃO DE MEMÓRIA			*
;*	*	*	*	*	*	*	*	*	*

; AQUI CRIAREMOS UM PSEUDÔNIMO PARA UM COMANDO. POR EXEMPLO, O COMANDO
; "BCF STATUS,RP0" PODE SER SUBSTITUÍDO SEM PROBLEMAS POR "BANK0". O 
; COMPILADOR IRÁ SUBSTITUIR INTERNAMENTE "BANK0" POR "BCF STATUS,RP0".
; ESTA APLICAÇÃO É VÁLIDA PARA OUTROS TIPOS DE COMANDOS

#DEFINE	PAGE0	BCF	PCLATH,3	; ESCOLHE BANCO DE MEMÓRIA 0
#DEFINE	PAGE1	BSF	PCLATH,3	; ESCOLHE BANCO DE MEMÓRIA 1

#DEFINE	BANK0	BCF	STATUS,RP0	; ESCOLHE BANCO DE MEMÓRIA 0
#DEFINE	BANK1	BSF	STATUS,RP0	; ESCOLHE BANCO DE MEMÓRIA 1

;*	*	*	*	*	*	*	*	*	*
;*	        DEFINIÇÃO DE RAM		*
;*	*	*	*	*	*	*	*	*	*

; AQUI IREMOS CRIAR AS VARIÁVEIS DO NOSSO SISTEMA

	CBLOCK 0x20	; INÍCIO DO BLOCO DE RAM(GPR)
		TEMP1
		CONTADOR
		TAM_PROG
		BYTE_RX
		TEMPO1
		TEMPO2					
		TEMPO3
		TEMPO4					
		DADO
		TEMP
		CARACTER
		BYTE0
		BYTE1
		BYTE2
		BYTE3
		BYTE4
		BYTE5
		BYTE6
		BYTE7
		WORD0_H
		WORD0_L
		WORD1_H
		WORD1_L
		WORD2_H
		WORD2_L
		WORD3_H
		WORD3_L
		WORD4_H
		WORD4_L
		WORD5_H
		WORD5_L
		WORD6_H
		WORD6_L
		WORD7_H
		WORD7_L		
		CONVERSAO_0
		CONVERSAO_1
		CONVERSAO_2
		CONVERSAO_3
		CONVERSAO_4
		LSB
		MSB
		AUX_VAR1
		AUX_VAR2
		AUX_VAR3
		AUX_VAR4
		AUX_VAR5
		AUX_VAR6
		AUX_VAR7
		AUX_VAR8
		RESULTADO_LO
		RESULTADO_HI
		MULTIPLICANDO_DIVISOR 
		MULTIPLICADOR_DIVIDENDO 
		MULTIPLICANDO_2
		FLAGS
		AUX_VAR1_FOR
		AUX_VAR2_FOR
		AUX_VAR3_FOR
		AUX_VAR4_FOR
		FSR_FOR
		ENDERECO_GOSUB
		
		
		
		
	ENDC		; FIM DO BLOCO DE RAM(GPR)
	
	CBLOCK 0x7E
		W_TEMP
		STATUS_TEMP
	ENDC
	
;*	*	*	*	*	*	*	*	*	*
;*       DEFINIÇÃO DE FLAGS			*
;*	*	*	*	*	*	*	*	*	*

; AQUI SÃO DEFINIDOS OS FLAGS QUE SERÃO UTILIZADOS PELO SISTEMA

#DEFINE		DADO_DISP	FLAGS,0		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO

#DEFINE		IN_OUT		FLAGS,1		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO
	
#DEFINE		CMD_REVERSE	FLAGS,2		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO

#DEFINE		TEM_BYTE	FLAGS,3		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO

#DEFINE		WHILE_MENORF	FLAGS,4		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO

#DEFINE		WHILE_MAIORF	FLAGS,5		;DEFINE O BIT 0 PARA FAZER A SELEÇÃO
						;DO DISPLAY OU DO CANAL SERIAL DE COMUNICAÇÃO

;*	*	*	*	*	*	*	*	*	*
;*	   DEFINICAO DE CONSTANTES		*
;*	*	*	*	*	*	*	*	*	*

; AQUI SÃO DEFINIDOS AS CONSTANTES QUE SERÃO UTILIZADOS PELO SISTEMA

;*	*	*	*	*	*	*	*	*	*
;*       ENTRADAS DO SISTEMA		*
;*	*	*	*	*	*	*	*	*	*

; AQUI SÃO DEFINIDAS AS ENTRADAS QUE SERÃO UTILIZADOS PELO SISTEMA
	
#DEFINE	IO1	PORTB,2
#DEFINE	IO2	PORTB,3
#DEFINE	IO3	PORTB,4
#DEFINE	IO4	PORTB,5
#DEFINE	IO5	PORTB,6
#DEFINE	IO6	PORTB,7
#DEFINE	IO7	PORTA,6
#DEFINE	IO8	PORTA,7

#DEFINE DB4	PORTB,4
#DEFINE	DB5	PORTB,5
#DEFINE	DB6	PORTB,6
#DEFINE	DB7	PORTB,7
#DEFINE	RX	PORTB,0
				
;*	*	*	*	*	*	*	*	*	*
;*	      SAIDAS DO SISTEMA			*
;*	*	*	*	*	*	*	*	*	*

; AQUI SÃO DEFINIDAS AS SAÍDAS QUE SERÃO UTILIZADOS PELO SISTEMA

#DEFINE	TX	PORTB,1
#DEFINE	RS	PORTA,0
#DEFINE ENABLE	PORTA,3
#DEFINE	DATA_	PORTA,0
#DEFINE CLOCK	PORTA,4

	ORG 0x2100
	DE 0xFE
	
;*	*	*	*	*	*	*	*	*	*
;*	        VETOR DE RESET			*
;*	*	*	*	*	*	*	*	*	*

	ORG 	0x0000		; ENDEREÇO INICIAL DA MÁQUINA
	MOVLW	B'00000101'
	MOVWF	CMCON		; DESLIGA TODOS OS COMPARADORES
	GOTO	CONFIG_		; SALTA PARA CONFIGURAR A MÁQUINA

;*	*	*	*	*	*	*	*	*	*
;*        VETOR DE INTERRUPÇÃO		*
;*	*	*	*	*	*	*	*	*	*

	ORG 	0x0004		; ENDEREÇO INICIAL DO VETOR DE INTERRUPÇÃO
	
	MOVWF	W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP
	CLRF	STATUS
	
	CALL	RECEBE_DADO 
	
	;MOVF	BYTE_RX,W
	;MOVWF	TEMP
	;CALL	TRANSMITE
		
	BCF	INTCON,INTF
	BSF	TEM_BYTE
			
	MOVF	BYTE_RX,W
	XORLW	.2
	BTFSS	STATUS,Z
	GOTO	SAI_INT
	
	INCF	CONTADOR,F
	MOVLW	.8
	XORWF	CONTADOR,W
	BTFSS	STATUS,Z
	GOTO	SAI_INT_A
	
ENTRA_PROG

	CLRF	CONTADOR
	
	MOVLW	.6
	MOVWF	TEMP
	CALL	TRANSMITE
	
	CLRWDT
	BTFSS	RX
	GOTO	$-2
	CALL	RECEBE_DADO
	
	MOVF	BYTE_RX,W
	MOVWF	TAM_PROG
	
	BANK1
	CLRF	EEADR
	BANK0

ENTRA_PROG_A
	
	CLRWDT
	BTFSS	RX
	GOTO	$-2
	CALL	RECEBE_DADO
	
	MOVF	BYTE_RX,W
	CALL	ESCREVE_EEPROM
	CALL	INC_PONTEIRO
	
	MOVLW	.6
	MOVWF	TEMP
	CALL	TRANSMITE
	
	INCF	CONTADOR
	MOVF	TAM_PROG,W
	XORWF	CONTADOR,W
	BTFSS	STATUS,Z
	GOTO	ENTRA_PROG_A
	
	BANK1
	MOVLW	B'01001000'
	MOVWF	OPTION_REG
	BANK0
	
	GOTO	$
	
SAI_INT
	CLRF	CONTADOR
SAI_INT_A		
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W
	RETFIE			; RETORNA DA INTERRUPÇÃO

RECEBE_DADO

	CALL	DELAY_104
	CALL	DELAY_52
	
	CLRF	BYTE_RX
	
	BTFSS	RX
	BSF	BYTE_RX,0
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,1
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,2
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,3
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,4
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,5
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,6
	CALL	DELAY_104
	
	BTFSS	RX
	BSF	BYTE_RX,7
	CALL	DELAY_104
	GOTO	DELAY_104


;*	*	*	*	*	*	*	*	*	*
;*	   CONFIGURAÇÕES INICIAIS		*
;*	*	*	*	*	*	*	*	*	*

CONFIG_
	
	
	CLRF	PORTA		; LIMPA O PORTA
	CLRF	PORTB		; LIMPA O PORTB
	
	BANK1			; SELECIONA BANCO1 DE MEMÓRIA

	MOVLW	B'00000110'
	MOVWF	TRISA		; CONFIGURA TODOS OS PINOS DESTE 
				; PORT COMO SAÍDA
	
	MOVLW	B'00000001'	; CONFIGURA TODOS OS PINOS DESTE
	MOVWF	TRISB		; PORT COMO SAÍDA EXCETO O PRIMEIRO PINO
	
	MOVLW	B'01001111'	; LIGA PULL-UPS
	MOVWF	OPTION_REG	; RESTANTE IRRELEVANTE NESTE EXEMPLO
	
	MOVLW	B'10010000'
	MOVWF	INTCON		; DESLIGA INTERRUPÇÕES

	MOVLW	.249
	MOVWF	PR2		; CONFIGURA PR2
	
	CLRF	EEADR
		
	BANK0

	
LIMPA_RAM

	MOVLW	0x20
	MOVWF	FSR
	
	CLRF	INDF
	INCF	FSR
	MOVLW	0x80
	XORWF	FSR,W
	BTFSS	STATUS,Z
	GOTO	$-5

	MOVLW	.255
	CALL	TEMPO_MS
	MOVLW	.255
	CALL	TEMPO_MS				

;*	*	*	*	*	*	*	*	*	*
;*  INICIALIZAÇÃO DAS VARIÁVEIS		*
;*	*	*	*	*	*	*	*	*	*

;*	*	*	*	*	*	*	*	*	*
;*        ROTINA PRINCIPAL			*
;*	*	*	*	*	*	*	*	*	*

LOOP
	
	CLRWDT
	CALL 	EXEC_PROG
	GOTO	LOOP
	

;*	*	*	*	*	*	*	*	*	*
;*      ROTINA DE TEMPO EM MS		*
;*	*	*	*	*	*	*	*	*	*

TEMPO_MS
	MOVWF	TEMPO2

TEMPO_MS1

	CLRWDT
	CLRF	TEMPO1
	DECFSZ	TEMPO1,F
	GOTO	$-1
	MOVLW	.75
	MOVWF	TEMPO1
	DECFSZ	TEMPO1,F
	GOTO	$-1
	DECFSZ	TEMPO2
	GOTO	TEMPO_MS1
	RETURN

;*	*	*	*	*	*	*	*	*	*
;*   ROTINA DE EXECUÇÃO DO PROGRAMA	*
;*	*	*	*	*	*	*	*	*	*

EXEC_PROG

	CALL	LE_EEPROM
	MOVWF	TEMP
	
	XORLW	.254
	BTFSC	STATUS,Z	
	GOTO	ENTRA_SLEEP
	
	MOVF	TEMP,W
	XORLW	.252
	BTFSC	STATUS,Z	
	GOTO	PROCURA_END_IF
	
	MOVF	TEMP,W
	XORLW	.253
	BTFSC	STATUS,Z	
	GOTO	INC_PONTEIRO
		
	MOVLW	HIGH EXEC_PROG_A
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  EXEC_PROG_A
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

EXEC_PROG_A
	
	GOTO	TROCA_DIR			;COMANDO PARA TROCAR A DIREÇÃO DOS PINOS
	GOTO	IOS					;ACESSO AOS PINOS DE I/O
	GOTO	DELAY_MS			;DELAY EM MS	
	GOTO	ALTERA_GOTO			;ALTERA O GOTO
	GOTO	TOGGLE				;ALTERA O ESTADO DO PORT
	GOTO 	SETAR_RESETAR_PINO	;COMANDO PARA SETAR E RESETAR O ESTADO DE UM PINO	
	GOTO	ATUALIZA_DISPLAY	;ROTINA PARA ATUALIZAR O DISPLAY
	GOTO	ALTERA_PWM			;ALTERAÇÃO DO PWM
	GOTO	TXDATA				;ROTINA DE TRANSMISSÃO SERIAL
	GOTO	IF_COMPARADOR		;VERIFICA O ESTADO DO COMPARADOR
	GOTO	IF_NOT_IO1			;TESTE DOS I/OS	
	GOTO	IF_NOT_IO2			;TESTE DOS I/OS
	GOTO	IF_NOT_IO3			;TESTE DOS I/OS	
	GOTO	IF_NOT_IO4			;TESTE DOS I/OS
	GOTO	IF_NOT_IO5			;TESTE DOS I/OS
	GOTO	IF_NOT_IO6			;TESTE DOS I/OS
	GOTO	IF_NOT_IO7			;TESTE DOS I/OS
	GOTO	IF_NOT_IO8			;TESTE DOS I/OS
	GOTO	IF_IO1				;TESTE DOS I/OS
	GOTO	IF_IO2				;TESTE DOS I/OS
	GOTO	IF_IO3				;TESTE DOS I/OS
	GOTO	IF_IO4				;TESTE DOS I/OS	
	GOTO	IF_IO5				;TESTE DOS I/OS	
	GOTO	IF_IO6				;TESTE DOS I/OS
	GOTO	IF_IO7				;TESTE DOS I/OS	
	GOTO	IF_IO8				;TESTE DOS I/OS
	GOTO	RECEP_SERIAL		;TESTA A RECEPÇÃO SERIAL
	GOTO	INIC_DISP			;INICIALIZA O DISPLAY
	GOTO	DADO_DISP1			;COMANDO DE ENVIO DE DADOS PARA O DISPLAY
	GOTO	LIMPAR_DISPLAY		;COMANDO PARA LIMPAR O DISPLAY
	GOTO	ROTA_DISP_ESQ		;ROTACIONAR O DISPLAY PARA A ESQUERDA
	GOTO	ROTA_DISP_DIR		;ROTACIONAR O DISPLAY PARA A DIREITA
	GOTO	AGUARDA_SEG			;ROTIAN PARA AGUARDAR TEMPO EM SEGUNDOS
	GOTO	IO1_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO2_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO3_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO4_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO5_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO6_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO7_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO8_VERDA			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO1_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO2_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO3_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO4_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO5_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO6_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO7_FALSO			;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	IO8_FALSO 							;AGUARDA ENTRADA 1 FICAR VERDADEIRA
	GOTO	ATRIBUI_VAR_BYTE					;ATRIBUI UM DADO A ALGUMA VARIÁVEL
	GOTO	ATRIBUI_VAR_WORD					;ATRIBUI UM DADO A ALGUMA VARIÁVEL
	GOTO	APRESENTA_BYTE_DISP					;APRESENTA A VARIÁVEL BYTE NO DISPLAY
	GOTO	APRESENTA_WORD_DISP					;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	SOMA_DUAS_VARIAVEIS_BYTE			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	SUBTRAI_DUAS_VARIAVEIS_BYTE			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	DIVIDE_DUAS_VARIAVEIS_BYTE			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	MULTIPLICA_DUAS_VARIAVEIS_BYTE		;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	SOMA_DUAS_VARIAVEIS_WORD			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	SUBTRAI_DUAS_VARIAVEIS_WORD			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	DIVIDE_DUAS_VARIAVEIS_WORD			;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	MULTIPLICA_DUAS_VARIAVEIS_WORD		;APRESENTA A VARIÁVEL WORD NO DISPLAY
	GOTO	INC_VAR_WORD			;INCREMENTA A VARIÁVEL DO TIPO WORD	
	GOTO	DEC_VAR_WORD			;INCREMENTA A VARIÁVEL DO TIPO WORD	
	GOTO	ENVIA_VAR_BYTE			;INCREMENTA A VARIÁVEL DO TIPO WORD	
	GOTO	ENVIA_VAR_WORD			;INCREMENTA A VARIÁVEL DO TIPO WORD
	GOTO	TESTA_IGUALDADE_VAR_B	;TESTA A IGUALDADE ENTRE VARIÁVEIS
	GOTO	TESTA_IGUALDADE_VAR_W	;TESTA A IGUALDADE ENTRE VARIÁVEIS		
	GOTO	TESTA_MAIOR_BYTE		;TESTA SE UMA VARIÁVEL É MAIOR QUE OUTRA EM BYTE
	GOTO	TESTA_MAIOR_WORD		;TESTA SE UMA VARIÁVEL É MAIOR QUE OUTRA EM BYTE
	GOTO	NOT_BYTE				;OPERAÇÃO NOT NA VARIÁVEL BYTE
	GOTO	NOT_WORD				;OPERAÇÃO NOT NA VARIÁVEL WORD
	GOTO	AND_BYTE				;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	AND_WORD				;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	OR_BYTE					;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	OR_WORD					;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	XOR_BYTE				;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	XOR_WORD				;OPERAÇÃO AND ENTRE DUAS VARIÁVEIS
	GOTO	PULSOUT					;COMANDO PULSOUT
	GOTO	PULSIN					;MEDIÇÃO DO PULSO
	GOTO	FOR_NEXT_B				;EXECUÇÃO DO FOR...NEXT
	GOTO	WHILE_B					;EXECUÇÃO DO FOR...NEXT
	GOTO	INC_BYTE				;INCREMENTA VARIÁVEL BYTE
	GOTO	DEC_BYTE				;DECREMENTA VARIÁVEL BYTE
	GOTO	EXEC_DO					;EXECUTAR O DO
	GOTO	LOOP_UNTIL				;EXECUTA O LOOP UNTIL
	GOTO	BYTE_REC_IO				;VARIÁVEL BYTE RECEBE VALOR DE ALGUM PINO
	GOTO	PINO_ENTRADA			;CONFIGURA ALGUM PINO COMO ENTRADA
	GOTO	PINO_SAIDA				;CONFIGURA ALGUM PINO COMO SAIDA
	GOTO	LE_PROG_EEPROM			;LÊ UM DADO DA EEPROM
	GOTO	ESCREVE_PROG_EEPROM		;ESCREVE UM DADO DA EEPROM
	GOTO	GOSUB					;TRATAMENTO DO COMANDO GOSUB
	GOTO	RANDOW					;GERA UM NÚMERO RANDÔMICO
	GOTO	REVERSE					;ALTERA O ESTADO DO PINO
	GOTO	TESTA_SERIAL			;ALTERA O ESTADO DO PINO
	GOTO	INC_PWM					;COMANDO PARA INCREMENTAR PWM
	GOTO	DEC_PWM					;COMANDO PARA DECREMENTAR PWM
	GOTO	LOAD_VAR_PWM			;CARREGA VARIÁVEL EM VAR
	GOTO	LOAD_PWM_VAR			;CARREGA O VALOR DE PWM NA VARIÁVEL
	GOTO	VERIFICA_VAR_MENOR_B	;VERIFICA SE UMA VARIÁVEL É MENOR QUE OUTRA
	GOTO	VERIFICA_VAR_MENOR_W	;VERIFICA SE UMA VARIÁVEL É MENOR QUE OUTRA
	GOTO	WHILE_MENOR				;WHILE MENOR...
	GOTO	WHILE_MAIOR				;WHILE MAIOR...
	GOTO	ROTATE_LEFT				;ROTACIONA VARIÁVEL BYTE PARA ESQUERDA
	GOTO	ROTATE_RIGHT				;ROTACIONA VARIÁVEL BYTE PARA DIREITA	
	GOTO	ROTATE_LEFT_W				;ROTACIONA VARIÁVEL BYTE PARA ESQUERDA
	GOTO	ROTATE_RIGHT_W				;ROTACIONA VARIÁVEL BYTE PARA DIREITA	

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*		ROTINA PARA ROTACIONAR VARIÁVEL WORD PARA ESQUERDA		*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

ROTATE_LEFT_W

	CALL	RETORNA_ENDERECO
	ADDLW	.1
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	TEMP

	
ROT_LEFT_W
	
	BCF	STATUS,C
	RLF	INDF,F
	DECF	FSR
	RLF	INDF,F
	INCF	FSR,F
	
	DECFSZ	TEMP,F
	GOTO	ROT_LEFT_W
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*		ROTINA PARA ROTACIONAR VARIÁVEL WORD PARA ESQUERDA		*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

ROTATE_RIGHT_W

	CALL	RETORNA_ENDERECO
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	TEMP
	
ROT_RIGHT_W
	
	BCF	STATUS,C
	RRF	INDF,F
	INCF	FSR
	RRF	INDF,F
	DECF	FSR,F
	
	DECFSZ	TEMP,F
	GOTO	ROT_RIGHT_W
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*		ROTINA PARA ROTACIONAR VARIÁVEL BYTE PARA ESQUERDA		*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

ROTATE_LEFT

	CALL	RETORNA_ENDERECO_B
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	BCF		STATUS,C
	RLF		INDF,F
	DECFSZ	TEMP,F
	GOTO	$-3
	GOTO	INC_PONTEIRO
			
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*		ROTINA PARA ROTACIONAR VARIÁVEL BYTE PARA ESQUERDA		*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

ROTATE_RIGHT

	CALL	RETORNA_ENDERECO_B
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	BCF		STATUS,C
	RRF		INDF,F
	DECFSZ	TEMP,F
	GOTO	$-3
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA TESTAR SE UMA VARIÁVEL É MAIOR QUE OUTRA EM WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

VERIFICA_VAR_MENOR_W

	CALL	COMUM_WORD
		
	MOVF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	SUBWF	AUX_VAR3
	BTFSC	STATUS,Z
	GOTO	$+4
	BTFSS	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
	INCF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	SUBWF	AUX_VAR4
	BTFSC	STATUS,Z
	GOTO	PROCURA_ELSE
	BTFSS	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA TESTAR SE UMA VARIÁVEL É MAIOR QUE OUTRA EM BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

VERIFICA_VAR_MENOR_B

	CALL	COMUM_BYTE
		
	SUBWF	AUX_VAR2,W
	BTFSC	STATUS,Z
	GOTO	PROCURA_ELSE
	BTFSS	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

	
;*	*	*	*	*	*	*	*	*	*	*
;*        CARREGA PWM NA VARIÁVEL 		*
;*	*	*	*	*	*	*	*	*	*	*

LOAD_PWM_VAR
	
	CALL	RETORNA_ENDERECO_B
	MOVWF	FSR
	
	MOVF	CCPR1L,W
	MOVWF	INDF
	
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*        CARREGA VARIÁVEL EM PWM 		*
;*	*	*	*	*	*	*	*	*	*	*

LOAD_VAR_PWM
	
	CALL	RETORNA_ENDERECO_B
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	CCPR1L
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*  	     INCREMENTO DE PWM 			*
;*	*	*	*	*	*	*	*	*	*	*

INC_PWM
	
	INCF	CCPR1L,W
	XORLW	.255
	BTFSS	STATUS,Z
	INCF	CCPR1L
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*  	     DECREMENTO DE PWM 			*
;*	*	*	*	*	*	*	*	*	*	*

DEC_PWM
	
	DECF	CCPR1L,W
	XORLW	.0
	BTFSS	STATUS,Z
	DECF	CCPR1L
	GOTO	INC_PONTEIRO

	
	
;*	*	*	*	*	*	*	*	*	*	*
;* 	   TESTE DA COMUNICAÇÃO SERIAL		*
;*	*	*	*	*	*	*	*	*	*	*

TESTA_SERIAL

	CALL	LE_DADO
	MOVWF	TEMP
	
	BTFSS	TEM_BYTE
	GOTO	PROCURA_ELSE
	
	MOVF	TEMP,W
	XORWF	BYTE_RX,W
	BTFSS	STATUS,Z
	GOTO	PROCURA_ELSE
	BCF	TEM_BYTE
	GOTO	INC_PONTEIRO
	

;*	*	*	*	*	*	*	*	*	*	*
;*      ALTERAÇÃO DO ESTADO DO PINO		*
;*	*	*	*	*	*	*	*	*	*	*

REVERSE

	BSF	CMD_REVERSE
		
	
;*	*	*	*	*	*	*	*	*	*	*
;*    GERAÇÃO DE NÚMERO RANDÔMICO		*
;*	*	*	*	*	*	*	*	*	*	*

RANDOW

	CALL	LE_DADO
	ADDLW	WORD0_H
	MOVWF	FSR
	
	XORWF	TMR2,W
	XORWF	INDF
		
	INCF	FSR,F
	MOVF	TMR2,W
	ADDWF	INDF,F
	
	GOTO	INC_PONTEIRO
	
	
;*	*	*	*	*	*	*	*	*	*	*
;*          TRATAMENTO DO GOSUB			*
;*	*	*	*	*	*	*	*	*	*	*

GOSUB

	BANK1	
	INCF	EEADR,W
	BANK0
	MOVWF	ENDERECO_GOSUB
	
	CALL	LE_DADO
	BANK1
	MOVWF	EEADR
	BANK0
	
GOSUB_A
		
	CALL	EXEC_PROG
	
	CALL	LE_EEPROM
	XORLW	.248
	BTFSS	STATUS,Z
	GOTO	GOSUB_A
	
	MOVF	ENDERECO_GOSUB,W
	BANK1
	MOVWF	EEADR
	BANK0
	GOTO	INC_PONTEIRO
	
	
;*	*	*	*	*	*	*	*	*	*	*
;*  ROTINA PARA LER UM VALOR DA EEPROM	*
;*	*	*	*	*	*	*	*	*	*	*

LE_PROG_EEPROM

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	BANK1
	MOVF	EEADR,W
	BANK0
	MOVWF	TEMP
	
	BANK1
	MOVLW	.255
	MOVWF	EEADR
	BANK0
	
	CALL	LE_EEPROM
	MOVWF	INDF
	
	MOVF	TEMP,W
	BANK1
	MOVWF	EEADR
	BANK0
	GOTO	INC_PONTEIRO	

;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA ESCREVER UM VALOR DA EEPROM*
;*	*	*	*	*	*	*	*	*	*	*

ESCREVE_PROG_EEPROM

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	BANK1
	MOVF	EEADR,W
	BANK0
	MOVWF	TEMP
	
	BANK1
	MOVLW	.255
	MOVWF	EEADR
	BANK0
	
	MOVF	INDF,W
	CALL	ESCREVE_EEPROM
	
	MOVF	TEMP,W
	BANK1
	MOVWF	EEADR
	BANK0
	GOTO	INC_PONTEIRO	
	
;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA CONFIGURAR O PINO COMO ENTRADA*
;*	*	*	*	*	*	*	*	*	*	*	*

PINO_ENTRADA

	BCF 	CMD_REVERSE
	BCF	IN_OUT

PINO_COMUM
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH BYTE_IO0
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  BYTE_IO0
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

BYTE_IO0
	
	GOTO	TESTA_IO1_INPUT
	GOTO	TESTA_IO2_INPUT
	GOTO	TESTA_IO3_INPUT
	GOTO	TESTA_IO4_INPUT
	GOTO	TESTA_IO5_INPUT
	GOTO	TESTA_IO6_INPUT
	GOTO	TESTA_IO7_INPUT
	GOTO	TESTA_IO8_INPUT
	
TESTA_IO1_INPUT
	
	
	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR1
	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,0
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,0
	BANK0
	GOTO	INC_PONTEIRO
		
TESTA_IO2_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR2
	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,1
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,1
	BANK0
	GOTO	INC_PONTEIRO
	
TESTA_IO3_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR3

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,2
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,2
	BANK0
	GOTO	INC_PONTEIRO

TESTA_IO4_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR4

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,3
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,3
	BANK0
	GOTO	INC_PONTEIRO

TESTA_IO5_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR5

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,4
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,4
	BANK0
	GOTO	INC_PONTEIRO

TESTA_IO6_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR6

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,5
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,5
	BANK0
	GOTO	INC_PONTEIRO
	
TESTA_IO7_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR7

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,6
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,6
	BANK0
	GOTO	INC_PONTEIRO

TESTA_IO8_INPUT

	BTFSC	CMD_REVERSE
	GOTO	ALTERA_DIR8

	BTFSC	IN_OUT
	GOTO	$+5
	BANK1
	BSF	TRISB,7
	BANK0
	GOTO	INC_PONTEIRO
	BANK1
	BCF	TRISB,7
	BANK0
	GOTO	INC_PONTEIRO

ALTERA_DIR1
	
	BANK1
	BTFSC	TRISB,0
	GOTO	$+3
	BSF	TRISB,0
	GOTO	INC_PONTEIRO
	BCF	TRISB,0
	GOTO	INC_PONTEIRO

ALTERA_DIR2
	
	BANK1
	BTFSC	TRISB,1
	GOTO	$+3
	BSF	TRISB,1
	GOTO	INC_PONTEIRO
	BCF	TRISB,1
	GOTO	INC_PONTEIRO
	
ALTERA_DIR3
	
	BANK1
	BTFSC	TRISB,2
	GOTO	$+3
	BSF	TRISB,2
	GOTO	INC_PONTEIRO
	BCF	TRISB,2
	GOTO	INC_PONTEIRO

ALTERA_DIR4
	
	BANK1
	BTFSC	TRISB,3
	GOTO	$+3
	BSF	TRISB,3
	GOTO	INC_PONTEIRO
	BCF	TRISB,3
	GOTO	INC_PONTEIRO
	
ALTERA_DIR5
	
	BANK1
	BTFSC	TRISB,4
	GOTO	$+3
	BSF	TRISB,4
	GOTO	INC_PONTEIRO
	BCF	TRISB,4
	GOTO	INC_PONTEIRO

ALTERA_DIR6
	
	BANK1
	BTFSC	TRISB,5
	GOTO	$+3
	BSF	TRISB,5
	GOTO	INC_PONTEIRO
	BCF	TRISB,5
	GOTO	INC_PONTEIRO

ALTERA_DIR7
	
	BANK1
	BTFSC	TRISB,6
	GOTO	$+3
	BSF	TRISB,6
	GOTO	INC_PONTEIRO
	BCF	TRISB,6
	GOTO	INC_PONTEIRO

ALTERA_DIR8
	
	BANK1
	BTFSC	TRISB,7
	GOTO	$+3
	BSF	TRISB,7
	GOTO	INC_PONTEIRO
	BCF	TRISB,7
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA CONFIGURAR O PINO COMO SAÍDA*
;*	*	*	*	*	*	*	*	*	*	*

PINO_SAIDA
	
	BCF 	CMD_REVERSE
	BSF		IN_OUT
	GOTO	PINO_COMUM
		
;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE LEITURA DE PINOS PARA VARIÁVEIS *
;*	*	*	*	*	*	*	*	*	*	* 	*

BYTE_REC_IO

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	CLRF	INDF		;INICIALIZA A VARIÁVEL
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH BYTE_IO
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  BYTE_IO
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

BYTE_IO
	
	GOTO	TESTA_IO1
	GOTO	TESTA_IO2
	GOTO	TESTA_IO3
	GOTO	TESTA_IO4
	GOTO	TESTA_IO5
	GOTO	TESTA_IO6
	GOTO	TESTA_IO7
	GOTO	TESTA_IO8
	
TESTA_IO1

	BTFSS	IO1
	INCF	INDF
	GOTO	INC_PONTEIRO
	
TESTA_IO2

	BTFSS	IO2
	INCF	INDF
	GOTO	INC_PONTEIRO
	
TESTA_IO3

	BTFSS	IO3
	INCF	INDF
	GOTO	INC_PONTEIRO

TESTA_IO4

	BTFSS	IO4
	INCF	INDF
	GOTO	INC_PONTEIRO

TESTA_IO5

	BTFSS	IO5
	INCF	INDF
	GOTO	INC_PONTEIRO
			
TESTA_IO6

	BTFSS	IO6
	INCF	INDF
	GOTO	INC_PONTEIRO

TESTA_IO7

	BTFSS	IO7
	INCF	INDF
	GOTO	INC_PONTEIRO
		
TESTA_IO8

	BTFSS	IO8
	INCF	INDF
	GOTO	INC_PONTEIRO
					
	
;*	*	*	*	*	*	*	*	*	*	*
;*   ROTINA DE EXECUÇÃO DO LOOP UNTIL	*
;*	*	*	*	*	*	*	*	*	*	*

LOOP_UNTIL
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	CALL	LE_DADO
	XORWF	INDF,W
	
	BTFSC	STATUS,Z
	GOTO	INC_PONTEIRO
	
	MOVF	AUX_VAR3_FOR,W
	BANK1
	MOVWF	EEADR
	BANK0
	GOTO	INC_PONTEIRO	 

;*	*	*	*	*	*	*	*	*	*	*
;*        ROTINA DE EXECUÇÃO DO DO 		*
;*	*	*	*	*	*	*	*	*	*	*

EXEC_DO
	
	BANK1
	MOVF	EEADR,W
	BANK0
	MOVWF	AUX_VAR3_FOR
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE INCREMENTO DE VARIÁVEL BYTE	*
;*	*	*	*	*	*	*	*	*	*	*

INC_BYTE

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	INCF	INDF,F
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;* ROTINA DE DECREMENTO DE VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*

DEC_BYTE

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	DECF	INDF,F
	GOTO	INC_PONTEIRO
	

;*	*	*	*	*	*	*	*	*	*	*
;* ROTINA DE EXECUÇÃO DO WHILE...WEND>	*
;*	*	*	*	*	*	*	*	*	*	*

WHILE_MAIOR
	
	BSF		WHILE_MAIORF
	BCF		WHILE_MENORF
	GOTO	WHILE_BB
	

;*	*	*	*	*	*	*	*	*	*	*
;* ROTINA DE EXECUÇÃO DO WHILE...WEND>	*
;*	*	*	*	*	*	*	*	*	*	*

WHILE_MENOR
	
	BCF		WHILE_MAIORF
	BSF		WHILE_MENORF
	GOTO	WHILE_BB
	
;*	*	*	*	*	*	*	*	*	*	*
;* ROTINA DE EXECUÇÃO DO WHILE...WEND=	*
;*	*	*	*	*	*	*	*	*	*	*

WHILE_B
	
	BCF		WHILE_MAIORF
	BCF		WHILE_MENORF

WHILE_BB
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	AUX_VAR1_FOR
	
	BANK1
	INCF	EEADR
	MOVF	EEADR,W
	BANK0
	
	MOVWF	AUX_VAR3_FOR
	
TESTE_WHILE_B
	
	MOVF	AUX_VAR3_FOR,W
	BANK1
	MOVWF	EEADR
	BANK0
	
	BTFSC	WHILE_MAIORF
	GOTO	CHECA_WHILE_MAIOR
	
	BTFSC	WHILE_MENORF
	GOTO	CHECA_WHILE_MENOR
	
	MOVF	INDF,W
	XORWF	AUX_VAR1_FOR,W
	BTFSC	STATUS,Z
	GOTO	PROCURA_WEND		
	GOTO	EXEC_WHILE_B

CHECA_WHILE_MENOR
	
	MOVF	INDF,W
	SUBWF	AUX_VAR1_FOR,W
	BTFSC	STATUS,Z
	GOTO	PROCURA_WEND		
	BTFSS	STATUS,C
	GOTO	PROCURA_WEND		
	GOTO	EXEC_WHILE_B
	
CHECA_WHILE_MAIOR
	
	MOVF	INDF,W
	SUBWF	AUX_VAR1_FOR,W
	BTFSC	STATUS,Z
	GOTO	PROCURA_WEND		
	BTFSC	STATUS,C
	GOTO	PROCURA_WEND		

EXEC_WHILE_B
	
	MOVF	FSR,W
	MOVWF	FSR_FOR
	
	CALL	EXEC_PROG
	
	MOVF	FSR_FOR,W
	MOVWF	FSR
	
	CALL	LE_EEPROM
	XORLW	.249		;CHEGOU NO NEXT?
	BTFSS	STATUS,Z
	GOTO	EXEC_WHILE_B		;EXECUTA NOVAMENTE O COMANDO
	GOTO	TESTE_WHILE_B	

;*	*	*	*	*	*	*	*	*	*
;*ROTINA DE PROCURA DO COMANDO WEND	*
;*	*	*	*	*	*	*	*	*	*

PROCURA_WEND

	CALL	LE_DADO
	XORLW	.249
	BTFSS	STATUS,Z
	GOTO	PROCURA_WEND
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*
;*   ROTINA DE EXECUÇÃO DO FOR...NEXT	*
;*	*	*	*	*	*	*	*	*	*	*

FOR_NEXT_B
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	
	CALL	LE_DADO
	MOVWF	INDF		;CARREGA O VALOR INICIAL NA VARIÁVEL
	
	CALL	LE_DADO
	ADDLW	.1
	MOVWF	AUX_VAR1_FOR
	
	CALL	LE_DADO
	MOVWF	AUX_VAR4_FOR
	
	BANK1
	INCF	EEADR
	MOVF	EEADR,W
	BANK0
	
	MOVWF	AUX_VAR3_FOR

FOR_NEXT_A
	
	MOVF	FSR,W
	MOVWF	FSR_FOR
	
	CALL	EXEC_PROG
	
	MOVF	FSR_FOR,W
	MOVWF	FSR
	
	CALL	LE_EEPROM
	XORLW	.250		;CHEGOU NO NEXT?
	BTFSS	STATUS,Z
	GOTO	FOR_NEXT_A	;EXECUTA NOVAMENTE O COMANDO
	
	MOVF	AUX_VAR4_FOR,W
	ADDWF	INDF,F
	MOVF	AUX_VAR1_FOR,W
	SUBWF	INDF,W
	BTFSC	STATUS,Z
	GOTO	INC_PONTEIRO
	BTFSS	STATUS,C
	GOTO	EXEC_NOVAMENTE	
	GOTO	INC_PONTEIRO

EXEC_NOVAMENTE	 
	
	MOVF	AUX_VAR3_FOR,W
	BANK1
	MOVWF	EEADR
	BANK0
	GOTO	FOR_NEXT_A

;*	*	*	*	*	*	*	*	*	*	*
;*     ROTINA DO COMANDO PULSIN			*
;*	*	*	*	*	*	*	*	*	*	*

PULSIN
	
	CALL	LE_DADO
	XORLW	.0
	
	BTFSS	STATUS,Z
	GOTO	TESTA_EM_1

TESTA_EM_0

	CALL	LOAD_VAR
	
	BTFSS	IO8
	GOTO	$-1
	
TESTA_EM_A

	MOVLW	.1
	CALL	DELAY_SEG
	
	INCF	INDF
	BTFSS	STATUS,Z
	GOTO	$+4	
	DECF	FSR
	INCF	INDF
	INCF	FSR
		
	BTFSC	IO8
	GOTO	TESTA_EM_A
	GOTO	INC_PONTEIRO	

TESTA_EM_1

	CALL	LOAD_VAR
	
	BTFSC	IO8
	GOTO	$-1
	
TESTA_EM_B

	MOVLW	.1
	CALL	DELAY_SEG
	
	INCF	INDF
	BTFSS	STATUS,Z
	GOTO	$+4	
	DECF	FSR
	INCF	INDF
	INCF	FSR
		
	BTFSS	IO8
	GOTO	TESTA_EM_B
	GOTO	INC_PONTEIRO	

LOAD_VAR
	
	CALL	RETORNA_ENDERECO
	MOVWF	FSR
	CLRF	INDF
	INCF	FSR
	CLRF	INDF
	
	RETURN	
	
;*	*	*	*	*	*	*	*	*	*	*
;*      ROTINA DO COMANDO PULSOUT		*
;*	*	*	*	*	*	*	*	*	*	*

PULSOUT
	
	CALL	LE_DADO
	MOVWF	AUX_VAR1
	
	CALL	LE_DADO
	MOVWF	AUX_VAR2

OSCILA
	
	BSF	IO7
	MOVF	AUX_VAR1,W
	CALL	TEMPO_MS
	
	BCF	IO7
	MOVF	AUX_VAR1,W
	CALL	TEMPO_MS
		
	DECFSZ	AUX_VAR2,F
	GOTO	OSCILA
	GOTO	INC_PONTEIRO
	


;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO AND NA VARIÁVEL WORD*
;*	*	*	*	*	*	*	*	*	*	*	*	*

XOR_WORD

	CALL	LOG_COMUM_W	
	
	MOVF	AUX_VAR1,W
	XORWF	AUX_VAR3,F
	MOVF	AUX_VAR2,W
	XORWF	AUX_VAR4,F
	
	GOTO	SALVA_OPER_W 

;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO XOR NA VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*

XOR_BYTE

	CALL	LOG_COMUM
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	MOVF	INDF,W
	XORWF	AUX_VAR2,F
	GOTO	SALVA_LOG_OPER

;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO AND NA VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*

LOG_COMUM

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	AUX_VAR1
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR2
	RETURN
	
OR_BYTE

	CALL	LOG_COMUM
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	MOVF	INDF,W
	IORWF	AUX_VAR2,F

SALVA_LOG_OPER
	
	MOVF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	AUX_VAR2,W
	MOVWF	INDF
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO AND NA VARIÁVEL WORD*
;*	*	*	*	*	*	*	*	*	*	*	*	*

LOG_COMUM_W

	CALL	RETORNA_ENDERECO
	MOVWF	TEMP
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	AUX_VAR1
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR2
		
	CALL	RETORNA_ENDERECO
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR3
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR4
	RETURN
	
OR_WORD

	CALL	LOG_COMUM_W
	
	MOVF	AUX_VAR1,W
	IORWF	AUX_VAR3,F
	MOVF	AUX_VAR2,W
	IORWF	AUX_VAR4,F
	
SALVA_OPER_W
	
	MOVF	TEMP,W
	MOVWF	FSR
	
	MOVF	AUX_VAR3,W
	MOVWF	INDF
	INCF	FSR
	MOVF	AUX_VAR4,W
	MOVWF	INDF	
	
	GOTO	INC_PONTEIRO


;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO AND NA VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*

AND_BYTE

	CALL	LOG_COMUM
	
	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	MOVF	INDF,W
	ANDWF	AUX_VAR2,F
	
	GOTO	SALVA_LOG_OPER
	
;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO AND NA VARIÁVEL WORD*
;*	*	*	*	*	*	*	*	*	*	*	*	*

AND_WORD

	CALL	LOG_COMUM_W	
	
	MOVF	AUX_VAR1,W
	ANDWF	AUX_VAR3,F
	MOVF	AUX_VAR2,W
	ANDWF	AUX_VAR4,F
	
	GOTO	SALVA_OPER_W 
	
;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO NOT NA VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*

NOT_BYTE

	CALL	LE_DADO
	ADDLW	BYTE0
	MOVWF	FSR
	COMF	INDF,F
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA APLICAR A OPERAÇÃO NOT NA VARIÁVEL WORD*
;*	*	*	*	*	*	*	*	*	*	*	*	*

NOT_WORD

	CALL	RETORNA_ENDERECO
	MOVWF	FSR
	COMF	INDF,F
	INCF	FSR
	COMF	INDF,F
	GOTO	INC_PONTEIRO

	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA TESTAR SE UMA VARIÁVEL É MAIOR QUE OUTRA EM WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

TESTA_MAIOR_WORD

	CALL	COMUM_WORD
		
	MOVF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	SUBWF	AUX_VAR3
	BTFSC	STATUS,Z
	GOTO	$+4
	BTFSC	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
	INCF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	SUBWF	AUX_VAR4
	BTFSC	STATUS,Z
	GOTO	PROCURA_ELSE
	BTFSC	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
		
COMUM_WORD

	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR1
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2

	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR3
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR4
		
	RETURN
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA TESTAR SE UMA VARIÁVEL É MAIOR QUE OUTRA EM BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

TESTA_MAIOR_BYTE

	CALL	COMUM_BYTE
		
	SUBWF	AUX_VAR2,W
	BTFSC	STATUS,C
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA VERIFICAR IGUALDADE ENTRE VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

TESTA_IGUALDADE_VAR_B

	CALL	COMUM_BYTE
	
	XORWF	AUX_VAR2,W
	BTFSS	STATUS,Z
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

COMUM_BYTE

	MOVLW	BYTE0
	MOVWF	AUX_VAR1
	MOVWF	AUX_VAR2
	
	CALL	LE_DADO
	ADDWF	AUX_VAR1
	
	CALL	LE_DADO
	ADDWF	AUX_VAR2,W

	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR2	
	
	MOVF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	
	RETURN
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA VERIFICAR IGUALDADE ENTRE VARIÁVEIS DO TIPO WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

TESTA_IGUALDADE_VAR_W

	CALL	COMUM_WORD
	
	MOVF	AUX_VAR1,W
	MOVWF	FSR
	MOVF	INDF,W
	XORWF	AUX_VAR3,W
	BTFSS	STATUS,Z
	GOTO	PROCURA_ELSE
	
	INCF	FSR,F
	MOVF	INDF,W
	XORWF	AUX_VAR4,W
	BTFSS	STATUS,Z
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE ENVIO DE VARIÁVEL DO TIPO WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*

ENVIA_VAR_WORD
	
	BSF	DADO_DISP
	GOTO	ENVIA_WORD_A
	
;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE ENVIO DE VARIÁVEL DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*

ENVIA_VAR_BYTE
	
	BSF	DADO_DISP
	GOTO	ENVIA_BYTE_A

;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE INCREMENTO DE VARIÁVEL DO TIPO WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*

INC_VAR_WORD

	 MOVLW	WORD0_H
 	 MOVWF	FSR
 	 
 	 CALL	LE_DADO
 	 MOVWF	TEMP
 	 RLF	TEMP,W
 	 ADDWF	FSR
 	 
 	 INCF	FSR,F
 	 INCF	INDF
 	 BTFSS	STATUS,Z
 	 GOTO	INC_PONTEIRO
 	 
 	 DECF	FSR
 	 INCF	INDF
 	 GOTO	INC_PONTEIRO 	 

;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE DECREMENTO DE VARIÁVEL DO TIPO WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*

DEC_VAR_WORD

	 MOVLW	WORD0_H
 	 MOVWF	FSR
 	 
 	 CALL	LE_DADO
 	 MOVWF	TEMP
 	 RLF	TEMP,W
 	 ADDWF	FSR
 	 
 	 INCF	FSR,F
 	 DECF	INDF
 	 BTFSS	STATUS,Z
 	 GOTO	INC_PONTEIRO
 	 
 	 DECF	FSR
 	 DECF	INDF
 	 GOTO	INC_PONTEIRO 	 
		
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*	       ROTINA PARA RETORNAR O ENDERECO DA VARIÁVEL WORD			*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

RETORNA_ENDERECO

	CALL	LE_DADO
	MOVWF	TEMP1
	
	MOVLW	HIGH R_END
	MOVWF	PCLATH
	MOVF	TEMP1,W
	ADDLW	LOW  R_END
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

R_END

	RETLW	WORD0_H
	RETLW	WORD1_H
	RETLW	WORD2_H
	RETLW	WORD3_H
	RETLW	WORD4_H
	RETLW	WORD5_H
	RETLW	WORD6_H
	RETLW	WORD7_H
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*	        ROTINA PARA SOMAR DUAS VARIÁVEIS DO TIPO BYTE			*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

SOMA_DUAS_VARIAVEIS_WORD
							
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	MOVF	AUX_VAR2,W	
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	AUX_VAR1
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR2
		
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	AUX_VAR3
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR4
	
	MOVF	AUX_VAR2,W
	ADDWF	AUX_VAR4
	BTFSC	STATUS,C
	INCF	AUX_VAR3
	
	MOVF	AUX_VAR1,W
	ADDWF	AUX_VAR3
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	
	MOVF	AUX_VAR3,W
	MOVWF	INDF
	
	INCF	FSR,F
	MOVF	AUX_VAR4,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA SUBTRAIR DUAS VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

SUBTRAI_DUAS_VARIAVEIS_WORD
							
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	MOVF	AUX_VAR2,W	
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	AUX_VAR1
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR2
		
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	AUX_VAR3
	INCF	FSR
	MOVF	INDF,W
	MOVWF	AUX_VAR4
	
	MOVF	AUX_VAR4,W
	SUBWF	AUX_VAR2
	BTFSS	STATUS,C
	DECF	AUX_VAR1
	
	MOVF	AUX_VAR3,W
	SUBWF	AUX_VAR1
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	
	MOVF	AUX_VAR1,W
	MOVWF	INDF
	
	INCF	FSR,F
	MOVF	AUX_VAR2,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA DIVIDIR DUAS VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

DIVIDE_DUAS_VARIAVEIS_WORD

	CALL	RETORNA_ENDERECO	
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	MOVF	AUX_VAR2,W	
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	MULTIPLICANDO_2	
	INCF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICADOR_DIVIDENDO
		
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	INCF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICANDO_DIVISOR

	CALL	DIVIDE

	MOVF	AUX_VAR5,W
	MOVWF	FSR
	MOVF	RESULTADO_HI,W
	MOVWF	INDF
	INCF	FSR
	MOVF	RESULTADO_LO,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA MULTIPLICAR DUAS VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

MULTIPLICA_DUAS_VARIAVEIS_WORD

	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	MOVF	AUX_VAR2,W
	MOVWF	FSR
	INCF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICANDO_DIVISOR
	
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	INCF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICADOR_DIVIDENDO
	
	PAGE1
	CALL	MULTIPLY
	PAGE0
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	MOVF	RESULTADO_HI,W
	MOVWF	INDF
	INCF	FSR
	MOVF	RESULTADO_LO,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO



;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*	       ROTINA PARA RETORNAR O ENDERECO DA VARIÁVEL BYTE			*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

RETORNA_ENDERECO_B

	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH R_END_B
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  R_END_B
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

R_END_B

	RETLW	BYTE0
	RETLW	BYTE1
	RETLW	BYTE2
	RETLW	BYTE3
	RETLW	BYTE4
	RETLW	BYTE5
	RETLW	BYTE6
	RETLW	BYTE7

;*	*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA SOMAR DUAS VARIÁVEIS DO TIPO BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*

SOMA_DUAS_VARIAVEIS_BYTE
							
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL	
	
	MOVF	AUX_VAR2,W	
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	TEMP
	
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	
	MOVF	INDF,W
	ADDWF	TEMP,F
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	
	MOVF	TEMP,W
	MOVWF	INDF
	
	GOTO	INC_PONTEIRO
	


;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA SUBTRAIR DUAS VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

SUBTRAI_DUAS_VARIAVEIS_BYTE
							
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
		
	MOVF	AUX_VAR2,W	
	MOVWF	FSR
	
	MOVF	INDF,W
	MOVWF	TEMP
	
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	
	MOVF	INDF,W
	SUBWF	TEMP,F
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	
	MOVF	TEMP,W
	MOVWF	INDF
	
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA DIVIDIR DUAS VARIÁVEIS DO TIPO BYTE	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

DIVIDE_DUAS_VARIAVEIS_BYTE

	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL	
	
	MOVF	AUX_VAR2,W
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICADOR_DIVIDENDO

	MOVF	AUX_VAR3,W
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICANDO_DIVISOR

	CLRF	MULTIPLICANDO_2
	
	CALL	DIVIDE

	MOVF	AUX_VAR5,W
	MOVWF	FSR
	MOVF	RESULTADO_LO,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO


;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA MULTIPLICAR DUAS VARIÁVEIS DO TIPO BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

MULTIPLICA_DUAS_VARIAVEIS_BYTE

	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR5
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR2		;ENDEREÇO DA SEGUNDA VARIÁVEL
	
	CALL	RETORNA_ENDERECO
	MOVWF	AUX_VAR3		;ENDEREÇO DA SEGUNDA VARIÁVEL
		
	MOVF	AUX_VAR2,W
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICANDO_DIVISOR
	
	MOVF	AUX_VAR3,W
	MOVWF	FSR
	MOVF	INDF,W
	MOVWF	MULTIPLICADOR_DIVIDENDO
	
	PAGE1
	CALL	MULTIPLY
	PAGE0
	
	MOVF	AUX_VAR5,W
	MOVWF	FSR
	MOVF	RESULTADO_LO,W
	MOVWF	INDF
		
	GOTO	INC_PONTEIRO


;*	*	*	*	*	*	*	*	*	*	*
;*	   ROTINA DE DIVISÃO DE 16 BITS		*
;*	*	*	*	*	*	*	*	*	*	*

DIVIDE
	CLRF	RESULTADO_HI
	CLRF	RESULTADO_LO
	
	MOVF	MULTIPLICANDO_DIVISOR,F
	BTFSC	STATUS,Z
	RETURN
	
	MOVF	MULTIPLICADOR_DIVIDENDO,F
	BTFSS	STATUS,Z
	GOTO	DIVIDE_A
	
	MOVF	MULTIPLICANDO_2,F
	BTFSC	STATUS,Z
	RETURN
	
DIVIDE_A

	MOVF	MULTIPLICANDO_DIVISOR,W
	SUBWF	MULTIPLICADOR_DIVIDENDO,F
	BTFSS	STATUS,C
	GOTO	DIVIDE_B

DIVIDE_C

	INCF	RESULTADO_LO,F
	BTFSC	STATUS,Z
	INCF	RESULTADO_HI,F
	GOTO	DIVIDE_A

DIVIDE_B

	MOVF	MULTIPLICANDO_2,F
	BTFSC	STATUS,Z
	RETURN
	DECF	MULTIPLICANDO_2,F
	GOTO	DIVIDE_C

;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA APRESENTAR NO DISPLAY A VARIÁVEL WORD	*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

APRESENTA_WORD_DISP
	
	BCF	DADO_DISP
	CALL	LE_DADO
	CALL	LCD_COMANDO

ENVIA_WORD_A
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH EXEC_PROG_AAA
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  EXEC_PROG_AAA
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

EXEC_PROG_AAA
	
	GOTO	APRESENTA_0W
	GOTO	APRESENTA_1W
	GOTO	APRESENTA_2W
	GOTO	APRESENTA_3W
	GOTO	APRESENTA_4W
	GOTO	APRESENTA_5W
	GOTO	APRESENTA_6W
	GOTO	APRESENTA_7W

APRESENTA_0W

	MOVF	WORD0_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD0_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B
	
APRESENTA_1W

	MOVF	WORD1_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD1_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B

APRESENTA_2W

	MOVF	WORD2_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD2_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B

APRESENTA_3W

	MOVF	WORD3_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD3_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B

APRESENTA_4W

	MOVF	WORD4_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD4_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B
	
APRESENTA_5W

	MOVF	WORD5_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD5_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B
	
APRESENTA_6W

	MOVF	WORD6_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD6_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B

APRESENTA_7W

	MOVF	WORD7_H,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	MSB
	MOVF	WORD7_L,W			;MOVE O CONTEÚDO DO BYTE PARA W
	MOVWF	LSB
	GOTO	COMUM_BYTE_DISP_B

;*	*	*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA APRESENTAR NO DISPLAY A VARIÁVEL BYTE*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*

APRESENTA_BYTE_DISP
	
	BCF	DADO_DISP
	CALL	LE_DADO
	CALL	LCD_COMANDO

ENVIA_BYTE_A
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH EXEC_PROG_AA
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  EXEC_PROG_AA
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

EXEC_PROG_AA
	
	GOTO	APRESENTA_0
	GOTO	APRESENTA_1
	GOTO	APRESENTA_2
	GOTO	APRESENTA_3
	GOTO	APRESENTA_4
	GOTO	APRESENTA_5
	GOTO	APRESENTA_6
	GOTO	APRESENTA_7
		
		
APRESENTA_0

	MOVF	BYTE0,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP

APRESENTA_1

	MOVF	BYTE1,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP

APRESENTA_2

	MOVF	BYTE2,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP

APRESENTA_3

	MOVF	BYTE3,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP

APRESENTA_4

	MOVF	BYTE4,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP
	
APRESENTA_5

	MOVF	BYTE5,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP
	
APRESENTA_6

	MOVF	BYTE6,W			;MOVE O CONTEÚDO DO BYTE PARA W
	GOTO	COMUM_BYTE_DISP

APRESENTA_7

	MOVF	BYTE7,W			;MOVE O CONTEÚDO DO BYTE PARA W
	
COMUM_BYTE_DISP


	MOVWF	LSB
	CLRF	MSB

COMUM_BYTE_DISP_B
	
	PAGE1
	CALL	BIN_TO_DEC
	PAGE0
	
	MOVF	CONVERSAO_4,W
	ADDLW	'0'
	CALL	ENVIA_DADO_DISP_SER
		
	MOVF	CONVERSAO_3,W
	ADDLW	'0'
	CALL	ENVIA_DADO_DISP_SER
	
	MOVF	CONVERSAO_2,W
	ADDLW	'0'
	CALL	ENVIA_DADO_DISP_SER
	
	MOVF	CONVERSAO_1,W
	ADDLW	'0'
	CALL	ENVIA_DADO_DISP_SER
	
	MOVF	CONVERSAO_0,W
	ADDLW	'0'
	CALL	ENVIA_DADO_DISP_SER
	
	GOTO	INC_PONTEIRO

ENVIA_DADO_DISP_SER
	
	MOVWF	TEMP
	BTFSS	DADO_DISP	
	CALL	ENVIA_DISP
	BTFSC	DADO_DISP	
	CALL	TRANSMITE
	RETURN

;*	*	*	*	*	*	*	*	*	*	*
;*   ATRIBUI UM VALOR A VARIÁVEL BYTE	*
;*	*	*	*	*	*	*	*	*	*	*

ATRIBUI_VAR_WORD
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH SELEC_VAR0
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  SELEC_VAR0
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

SELEC_VAR0
	
	GOTO	TROCA_WORD0
	GOTO	TROCA_WORD1
	GOTO	TROCA_WORD2
	GOTO	TROCA_WORD3
	GOTO	TROCA_WORD4
	GOTO	TROCA_WORD5
	GOTO	TROCA_WORD6
	GOTO	TROCA_WORD7	
	
TROCA_WORD0

	CALL	LE_DADO
	MOVWF	WORD0_H
	CALL	LE_DADO
	MOVWF	WORD0_L
	GOTO	INC_PONTEIRO
	
TROCA_WORD1

	CALL	LE_DADO
	MOVWF	WORD1_H
	CALL	LE_DADO
	MOVWF	WORD1_L
	GOTO	INC_PONTEIRO	
TROCA_WORD2

	CALL	LE_DADO
	MOVWF	WORD2_H
	CALL	LE_DADO
	MOVWF	WORD2_L
	GOTO	INC_PONTEIRO	
	
TROCA_WORD3

	CALL	LE_DADO
	MOVWF	WORD3_H
	CALL	LE_DADO
	MOVWF	WORD3_L
	GOTO	INC_PONTEIRO

TROCA_WORD4

	CALL	LE_DADO
	MOVWF	WORD4_H
	CALL	LE_DADO
	MOVWF	WORD4_L
	GOTO	INC_PONTEIRO
			
TROCA_WORD5

	CALL	LE_DADO
	MOVWF	WORD5_H
	CALL	LE_DADO
	MOVWF	WORD5_L
	GOTO	INC_PONTEIRO	
	
TROCA_WORD6

	CALL	LE_DADO
	MOVWF	WORD6_H
	CALL	LE_DADO
	MOVWF	WORD6_L
	GOTO	INC_PONTEIRO	
	
TROCA_WORD7

	CALL	LE_DADO
	MOVWF	WORD7_H
	CALL	LE_DADO
	MOVWF	WORD7_L
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*
;*	 ATRIBUI UM VALOR A VARIÁVEL BYTE	*
;*	*	*	*	*	*	*	*	*	*	*

ATRIBUI_VAR_BYTE
	
	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	HIGH SELEC_VAR
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  SELEC_VAR
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

SELEC_VAR	
	
	GOTO	TROCA_VAR0
	GOTO	TROCA_VAR1
	GOTO	TROCA_VAR2
	GOTO	TROCA_VAR3
	GOTO	TROCA_VAR4
	GOTO	TROCA_VAR5
	GOTO	TROCA_VAR6
	GOTO	TROCA_VAR7	
	
TROCA_VAR0

	CALL	LE_DADO
	MOVWF	BYTE0
	GOTO	INC_PONTEIRO
	
TROCA_VAR1

	CALL	LE_DADO
	MOVWF	BYTE1
	GOTO	INC_PONTEIRO
	
TROCA_VAR2

	CALL	LE_DADO
	MOVWF	BYTE2
	GOTO	INC_PONTEIRO
	
TROCA_VAR3

	CALL	LE_DADO
	MOVWF	BYTE3
	GOTO	INC_PONTEIRO	

TROCA_VAR4

	CALL	LE_DADO
	MOVWF	BYTE4
	GOTO	INC_PONTEIRO		
	
TROCA_VAR5

	CALL	LE_DADO
	MOVWF	BYTE5
	GOTO	INC_PONTEIRO
	
TROCA_VAR6

	CALL	LE_DADO
	MOVWF	BYTE6
	GOTO	INC_PONTEIRO
	
TROCA_VAR7

	CALL	LE_DADO
	MOVWF	BYTE7
	GOTO	INC_PONTEIRO	
		
;*	*	*	*	*	*	*	*	*	*	*
;*COMANDO PARA AGUARDAR UM NÍVEL NO BOTÃO*
;*	*	*	*	*	*	*	*	*	*	*

IO1_VERDA
	
	CLRWDT
	BTFSC	IO1
	GOTO	IO1_VERDA
	GOTO	INC_PONTEIRO
	
IO2_VERDA
	
	CLRWDT
	BTFSC	IO2
	GOTO	IO2_VERDA
	GOTO	INC_PONTEIRO

IO3_VERDA

	CLRWDT
	BTFSC	IO3
	GOTO	IO3_VERDA
	GOTO	INC_PONTEIRO
	
IO4_VERDA

	CLRWDT
	BTFSC	IO4
	GOTO	IO4_VERDA
	GOTO	INC_PONTEIRO
	
IO5_VERDA

	CLRWDT
	BTFSC	IO5
	GOTO	IO5_VERDA
	GOTO	INC_PONTEIRO
	
IO6_VERDA

	CLRWDT
	BTFSC	IO6
	GOTO	IO6_VERDA
	GOTO	INC_PONTEIRO

IO7_VERDA

	CLRWDT
	BTFSC	IO7
	GOTO	IO7_VERDA
	GOTO	INC_PONTEIRO	

IO8_VERDA

	CLRWDT
	BTFSC	IO8
	GOTO	IO8_VERDA
	GOTO	INC_PONTEIRO	
	
IO1_FALSO

	CLRWDT
	BTFSS	IO1
	GOTO	IO1_FALSO
	GOTO	INC_PONTEIRO
	
IO2_FALSO

	CLRWDT
	BTFSS	IO2
	GOTO	IO2_FALSO
	GOTO	INC_PONTEIRO

IO3_FALSO

	CLRWDT
	BTFSS	IO3
	GOTO	IO3_FALSO
	GOTO	INC_PONTEIRO
	
IO4_FALSO

	CLRWDT
	BTFSS	IO4
	GOTO	IO4_FALSO
	GOTO	INC_PONTEIRO
	
IO5_FALSO

	CLRWDT
	BTFSS	IO5
	GOTO	IO5_FALSO
	GOTO	INC_PONTEIRO
	
IO6_FALSO

	CLRWDT
	BTFSS	IO6
	GOTO	IO6_FALSO
	GOTO	INC_PONTEIRO

IO7_FALSO

	CLRWDT
	BTFSS	IO7
	GOTO	IO7_FALSO
	GOTO	INC_PONTEIRO	

IO8_FALSO

	CLRWDT
	BTFSS	IO8
	GOTO	IO8_FALSO
	GOTO	INC_PONTEIRO	
		
;*	*	*	*	*	*	*	*	*	*	*
;*    ROTINA DE DELAY EM SEGUNDOS		*
;*	*	*	*	*	*	*	*	*	*	*

AGUARDA_SEG

	CALL	LE_DADO
	CALL	DELAY_SEG
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*	COMANDO PARA  ROTACIONAR O DISPLAY	*
;*	*	*	*	*	*	*	*	*	*	*

ROTA_DISP_ESQ

	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	0x18
	CALL	LCD_COMANDO
	
	DECFSZ	TEMP,F
	GOTO	$-3
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*
;*  COMANDO PARA  ROTACIONAR O DISPLAY	*
;*	*	*	*	*	*	*	*	*	*	*

ROTA_DISP_DIR

	CALL	LE_DADO
	MOVWF	TEMP
	
	MOVLW	0x1C
	CALL	LCD_COMANDO
	
	DECFSZ	TEMP,F
	GOTO	$-3
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*
;*   COMANDO PARA LIMPAR O DISPLAY		*
;*	*	*	*	*	*	*	*	*	*	*

LIMPAR_DISPLAY

	MOVLW	.1
	CALL	LCD_COMANDO
	GOTO	INC_PONTEIRO
	
	
;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE ENVIO DE DADOS PARA O DISPLAY*
;*	*	*	*	*	*	*	*	*	*	*

DADO_DISP1

	CALL	LE_DADO
	CALL	LCD_COMANDO		;POSICIONA O CURSOR DO DISPLAY

DADO_DISP_A
	
	CALL	LE_DADO
	MOVWF	TEMP
	XORLW	.255
	BTFSC	STATUS,Z
	GOTO	INC_PONTEIRO
	MOVF	TEMP,W
	CALL	ENVIA_DISP
	GOTO	DADO_DISP_A

;*	*	*	*	*	*	*	*	*	*	*
;*  ENVIO DE DADOS PARA LCD DE 4 BITS	*
;*	*	*	*	*	*	*	*	*	*	*

LCD_COMANDO_4					; ENVIA COMANDOS DE 4 BITS
						; NIBBLE DE D3..D0 (-SIG)

	MOVWF	CARACTER			; CARREGA COMANDO QUE SERÁ ENVIADO
	BCF	RS
	GOTO	ENVIA_DISP_4			; ENVIA...

LCD_COMANDO
	
	MOVWF	CARACTER
	BCF	RS
	GOTO	ENVIA_DISP_A
	
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*		  		 ENVIO DE DADOS PARA LCD					*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

ENVIA_DISP
	
	MOVWF	CARACTER
	BSF	RS

ENVIA_DISP_A
	
	BCF	DB7
	BTFSC	CARACTER,7
	BSF	DB7

	BCF	DB6
	BTFSC	CARACTER,6
	BSF	DB6

	BCF	DB5
	BTFSC	CARACTER,5
	BSF	DB5

	BCF	DB4
	BTFSC	CARACTER,4
	BSF	DB4					; COPIA BITS +SIG DE CARACTER PARA D7..D4

	BSF	ENABLE
	CALL    DELAY_DISP2		; CHAMA DELAY DE +/- 150us
	BCF	ENABLE				; PROVOCA PULSO NO ENABLE	

ENVIA_DISP_4				; ENVIA SOMENTE OS 4 BITS (-SIG) DO REG. E_LCD_VAR

	BCF	DB7
	BTFSC	CARACTER,3
	BSF	DB7

	BCF	DB6
	BTFSC	CARACTER,2
	BSF	DB6

	BCF	DB5
	BTFSC	CARACTER,1
	BSF	DB5

	BCF	DB4
	BTFSC	CARACTER,0
	BSF	DB4					; COPIA BITS -SIG DE E_LCD_VAR PARA D7..D4

	BSF	ENABLE
	CALL	DELAY_DISP2			; CHAMA DELAY DE +/- 150us
	BCF	ENABLE				; PROVOCA PULSO NO ENABLE	

	BSF	RS					; SELECIONA PADRÃO DE ENVIO DE UM CARACTER
	GOTO	DELAY_DISP	

;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*
;*			  		  ROTINA DE DELAY						*
;*	*	*	*	*	*	*	*	*	*	*	*	*	*	*	*

DELAY_DISP
	
	MOVLW	.15
	GOTO	TEMPO_MS	
	
DELAY_SEG
	MOVWF	TEMPO1
	
	MOVLW	.255
	MOVWF	TEMPO2
	MOVWF	TEMPO3
	MOVLW	.5
	MOVWF	TEMPO4
	
VERIFICA_PROG
	
	CLRWDT
	DECFSZ	TEMPO2,F
	GOTO	$-1
	DECFSZ	TEMPO3,F
	GOTO	VERIFICA_PROG
	DECFSZ	TEMPO4,F
	GOTO	VERIFICA_PROG
	MOVLW	.5
	MOVWF	TEMPO4
	DECFSZ	TEMPO1,F
	GOTO	VERIFICA_PROG
 	RETURN	

DELAY_DISP2
	MOVLW	.150
	MOVWF	TEMPO1
	
	DECFSZ	TEMPO1,F
	GOTO	$-1
	RETURN

;*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA INICIALIZA O DISPLAY	*
;*	*	*	*	*	*	*	*	*	*

INIC_DISP
         
         MOVLW	B'00000011'
         CALL	LCD_COMANDO_4
         MOVLW	B'00000011'
         CALL	LCD_COMANDO_4
         MOVLW	B'00000010'
         CALL	LCD_COMANDO_4
         
         MOVLW	B'00101000'
         CALL	LCD_COMANDO
         MOVLW	B'00001100'
         CALL	LCD_COMANDO
         MOVLW	B'00000001'
         CALL	LCD_COMANDO
         
 	 GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*	   ROTINA PARA RECEBER DADOS	*
;*	*	*	*	*	*	*	*	*	*

RECEP_SERIAL
	
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA ENTRAR EM MODO SLEEP	*
;*	*	*	*	*	*	*	*	*	*

ENTRA_SLEEP
	
	SLEEP
	GOTO	ENTRA_SLEEP
		
;*	*	*	*	*	*	*	*	*	*	*
;* ROTINA DE TESTE DAS ENTRADAS DIGITAIS*
;*	*	*	*	*	*	*	*	*	*	*

IF_NOT_IO1

	BTFSS	IO1
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
IF_NOT_IO2

	BTFSS	IO2
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_NOT_IO3

	BTFSS	IO3
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_NOT_IO4

	BTFSS	IO4
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_NOT_IO5

	BTFSS	IO5
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
IF_NOT_IO6

	BTFSS	IO6
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_NOT_IO7

	BTFSS	IO7
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_NOT_IO8

	BTFSS	IO8
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO


IF_IO1

	BTFSC	IO1
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
IF_IO2

	BTFSC	IO2
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_IO3

	BTFSC	IO3
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_IO4

	BTFSC	IO4
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_IO5

	BTFSC	IO5
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO
	
IF_IO6

	BTFSC	IO6
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_IO7

	BTFSC	IO7
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

IF_IO8

	BTFSC	IO8
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*ROTINA DE VERIFICAÇÃO DO COMPARADOR*
;*	*	*	*	*	*	*	*	*	*

IF_COMPARADOR

	BTFSS	CMCON,C2OUT
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;* ROTINA DE PROCURA DO COMANDO ELSE*	
;*	*	*	*	*	*	*	*	*	*

PROCURA_ELSE

	CALL	LE_DADO
	XORLW	.252
	BTFSS	STATUS,Z
	GOTO	PROCURA_ELSE
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*ROTINA DE PROCURA DO COMANDO END IF*
;*	*	*	*	*	*	*	*	*	*

PROCURA_END_IF

	CALL	LE_DADO
	XORLW	.253
	BTFSS	STATUS,Z
	GOTO	PROCURA_END_IF
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*    ROTINA DE TRANSMISSÃO SERIAL	*
;*	*	*	*	*	*	*	*	*	*

TXDATA

	CALL	LE_DADO
	MOVWF	TEMP
	XORLW	.255
	BTFSC	STATUS,Z
	GOTO	INC_PONTEIRO
	CALL	TRANSMITE
	GOTO	TXDATA

;*	*	*	*	*	*	*	*	*	*
;*	 ROTINA DE TRANSMISSÃO SERIAL	*
;*	*	*	*	*	*	*	*	*	*

TRANSMITE

	BSF	TX
	CALL	DELAY_104
	
	BTFSC	TEMP,0
	BCF	TX
	BTFSS	TEMP,0
	BSF	TX	
	CALL	DELAY_104
	
	BTFSC	TEMP,1
	BCF	TX
	BTFSS	TEMP,1
	BSF	TX	
	CALL	DELAY_104
	
	BTFSC	TEMP,2
	BCF	TX
	BTFSS	TEMP,2
	BSF	TX	
	CALL	DELAY_104

	BTFSC	TEMP,3
	BCF	TX
	BTFSS	TEMP,3
	BSF	TX	
	CALL	DELAY_104

	BTFSC	TEMP,4
	BCF	TX
	BTFSS	TEMP,4
	BSF	TX	
	CALL	DELAY_104

	BTFSC	TEMP,5
	BCF	TX
	BTFSS	TEMP,5
	BSF	TX	
	CALL	DELAY_104

	BTFSC	TEMP,6
	BCF	TX
	BTFSS	TEMP,6
	BSF	TX	
	CALL	DELAY_104

	BTFSC	TEMP,7
	BCF	TX
	BTFSS	TEMP,7
	BSF	TX	
	CALL	DELAY_104

	BCF	TX
	GOTO	DELAY_104

;*	*	*	*	*	*	*	*	*	*
;* DELAY PARA TRANSMISSÃO DE 1 BIT	*		
;*	*	*	*	*	*	*	*	*	*

DELAY_52

	MOVLW	.16
	MOVWF	TEMPO1
	DECFSZ	TEMPO1,F
	GOTO	$-1
	RETURN
	
;*	*	*	*	*	*	*	*	*	*
;*  DELAY PARA TRANSMISSÃO DE 1 BIT	*		
;*	*	*	*	*	*	*	*	*	*

DELAY_104

	MOVLW	.33
	MOVWF	TEMPO1
	DECFSZ	TEMPO1,F
	GOTO	$-1
	RETURN
			
;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA ALTERAR O CICLO ATIVO DO PWM	*
;*	*	*	*	*	*	*	*	*	*	*	*

ALTERA_PWM

	MOVLW	B'00000101'
	MOVWF	T2CON		; PRESCALER 1:4
				; TIMER2 ON
				; FREQUENCIA DE PMW 1kHz	
	
	MOVLW	B'00001111'
	MOVWF	CCP1CON		; CONFIGURA PARA FUNCIONAR EM MODO PWM
	
	CALL	LE_DADO
	MOVWF	CCPR1L		; GARANTE DUTY-CICLE EM 0

	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA CONFIGURAR A DIREÇÃO DO PINO	*
;*	*	*	*	*	*	*	*	*	*	*	*
					
TROCA_DIR

	CALL	LE_DADO
	MOVWF	AUX_VAR1
	MOVWF	AUX_VAR2
	
	RLF	AUX_VAR1,F
	RLF	AUX_VAR1,F
		
	BANK1
	MOVLW	B'00000011'
	ANDWF	TRISB
	BANK0
	
	MOVF	AUX_VAR1,W
	ANDLW	B'11111100'
	BANK1
	IORWF	TRISB
	MOVLW	B'00111111'
	ANDWF	TRISA
	BANK0
	
	MOVF	AUX_VAR2,W
	ANDLW	B'11000000'
	BANK1
	IORWF	TRISA
	BANK0
	
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA CARREGAR UM VALOR NOS I/OS*
;*	*	*	*	*	*	*	*	*	*	*
	
IOS

	CALL	LE_DADO
	MOVWF	AUX_VAR1
	MOVWF	AUX_VAR2
	
	MOVLW	B'00000011'
	ANDWF	PORTB
	RLF	AUX_VAR1,F
	RLF	AUX_VAR1,W
	IORWF	PORTB
	
	MOVLW	B'00111111'
	ANDWF	PORTA
	
	MOVF	AUX_VAR2,W
	IORWF	PORTA,F
	
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*	    ROTINA DE TEMPO EM MS		*
;*	*	*	*	*	*	*	*	*	*

DELAY_MS

	CALL	LE_DADO
	CALL	TEMPO_MS
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA SALTAR PARA UMA LINHA DO DISPLAY	*
;*	*	*	*	*	*	*	*	*	*	*	*	*

ALTERA_GOTO

	CALL	LE_DADO
	BANK1
	MOVWF	EEADR
	BANK0
	RETURN

;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA INVERTER O ESTADO DO PINO	*
;*	*	*	*	*	*	*	*	*	*	*

TOGGLE
	
	COMF	PORTB,W
	MOVWF	TEMP
		
	MOVLW	B'00000011'
	ANDWF	PORTB,F
	
	MOVF	TEMP,W
	ANDLW	B'11111100'
	IORWF	PORTB
	
	COMF	PORTA,W
	MOVWF	TEMP
		
	MOVLW	B'00111111'
	ANDWF	PORTA,F
	
	MOVF	TEMP,W
	ANDLW	B'11111100'
	IORWF	PORTA
		
	GOTO	INC_PONTEIRO

;*	*	*	*	*	*	*	*	*	*
;*ROTINA DE ACESSO AOS PINOS DE I/O	*
;*	*	*	*	*	*	*	*	*	*
		
SETAR_RESETAR_PINO
	
	CALL	LE_DADO
	MOVWF	TEMP
	MOVLW	HIGH SALTAR_PINO
	MOVWF	PCLATH
	MOVF	TEMP,W
	ADDLW	LOW  SALTAR_PINO
	BTFSC	STATUS,C
	INCF	PCLATH
	MOVWF	PCL	

SALTAR_PINO

	GOTO	SET1
	GOTO	SET2
	GOTO	SET3
	GOTO	SET4
	GOTO	SET5
	GOTO	SET6
	GOTO	SET7
	GOTO	SET8	
	GOTO	RESET1
	GOTO	RESET2
	GOTO	RESET3
	GOTO	RESET4
	GOTO	RESET5
	GOTO	RESET6
	GOTO	RESET7
	GOTO	RESET8	

SET1
	
	BSF		IO1
	GOTO	INC_PONTEIRO
	
SET2
	
	BSF		IO2
	GOTO	INC_PONTEIRO
	
SET3
	
	BSF		IO3
	GOTO	INC_PONTEIRO

SET4
	
	BSF		IO4
	GOTO	INC_PONTEIRO

SET5
	
	BSF		IO5
	GOTO	INC_PONTEIRO

SET6
	
	BSF		IO6
	GOTO	INC_PONTEIRO

SET7
	
	BSF		IO7
	GOTO	INC_PONTEIRO

SET8
	
	BSF		IO8
	GOTO	INC_PONTEIRO

RESET1
	
	BCF		IO1
	GOTO	INC_PONTEIRO
	
RESET2
	
	BCF		IO2
	GOTO	INC_PONTEIRO
	
RESET3
	
	BCF		IO3
	GOTO	INC_PONTEIRO

RESET4
	
	BCF		IO4
	GOTO	INC_PONTEIRO

RESET5
	
	BCF		IO5
	GOTO	INC_PONTEIRO

RESET6
	
	BCF		IO6
	GOTO	INC_PONTEIRO

RESET7
	
	BCF		IO7
	GOTO	INC_PONTEIRO

RESET8
	
	BCF		IO8
	GOTO	INC_PONTEIRO


;*	*	*	*	*	*	*	*	*	*
;*			ROTINA PARA ATUALIZAÇÃO DO DISPLAY		*
;*	*	*	*	*	*	*	*	*	*

ATUALIZA_DISPLAY
	
	CALL	LE_DADO
	MOVWF	DADO		; MOVE DADO QUE VEM DE W PARA DADO
	MOVLW	.8		; MOVE W COM 8
	MOVWF	TEMP1		; CARREGA TEMP
	
ATUALIZA_DISPLAY_A

	RLF	DADO,F		; FAZ UMA ROTAÇÃO A ESQUERDA DE DADO
				; E ATUALIZA FLAG C DO REG STATUS
		
	BTFSC	STATUS,C	; FLAG C IGUAL A ZERO?	
	BSF	DATA_		; SETA DATA_
	BTFSS	STATUS,C	; FLAG C IGUAL A UM?
	BCF	DATA_		; ZERA DATA_
	
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	
	BSF	CLOCK		 ;GERA PULSO DE CLOCK
	NOP
	NOP
	NOP
	BCF	CLOCK
	
	DECFSZ	TEMP1,F		; REPETE ESTE LOOP 8 VEZES
	GOTO	ATUALIZA_DISPLAY_A	; FICA EM LOOP ATÉ TEMP CHEGA A ZERO		
	GOTO	INC_PONTEIRO
	
;*	*	*	*	*	*	*	*	*	*
;* ROTINA DE INCREMENTO DE PONTEIRO	*
;*	*	*	*	*	*	*	*	*	*

INC_PONTEIRO

	BANK1
	INCF	EEADR
	BANK0
	RETURN
		
;*	*	*	*	*	*	*	*	*	*	*
;*ROTINA PARA ESCREVER DADOS DA EEPROM	*
;*	*	*	*	*	*	*	*	*	*	*

ESCREVE_EEPROM
	
	BANK1				; SELECIONA BANCO1
	MOVWF	EEDATA		; CARREGA DADO EM EEDATA
	BSF		EECON1,WREN	; HABILITA A ESCRITA
	MOVLW	0x55		; INICIALIZA A ESCRITA	
	MOVWF	EECON2
	MOVLW	0XAA
	MOVWF	EECON2
	BSF		EECON1,WR	; INICIA ESCRITA
	BCF		EECON1,WREN	; DESABILITA A ESCRITA
	
	BTFSC	EECON1,WR	; ACABOU A ESCRITA?
	GOTO	$-1			; NÃO, ENTÃO AGUARDA
	BANK0				; SELECIONA BANCO0
	RETURN				; RETORNA DA SUB-ROTINA

;*	*	*	*	*	*	*	*	*	*
;* ROTINA PARA LER DADOS DA EEPROM	*
;*	*	*	*	*	*	*	*	*	*

LE_DADO
	BANK1
	INCF	EEADR,F


LE_EEPROM
	
	BANK1				; SELECIONA BANCO1
	BSF		EECON1,RD	; SETA BIT DE LEITURA
	MOVF	EEDATA,W	; MOVE DADO LIDO PARA W
	BANK0				; SELECIONA BANCO 0
	RETURN				; RETORNA DA ROTINA


	
	org	 0X7FF	
	org	 0X800
	
;*	*	*	*	*	*	*	*	*	*	*
;*  ROTINA DE MULTIPLICAÇÃO DE 16 BITS	*		
;*	*	*	*	*	*	*	*	*	*	*

MULTIPLY

	CLRF	RESULTADO_LO
	CLRF	RESULTADO_HI
	
	
	MOVF	MULTIPLICANDO_DIVISOR,F 
	BTFSC	STATUS,Z
	RETURN
	
	MOVF	MULTIPLICADOR_DIVIDENDO,F 
	BTFSS	STATUS,Z
	GOTO	MULTIPLY_A	
	
	MOVF	MULTIPLICANDO_2,F
	BTFSC	STATUS,Z	
	RETURN
	
MULTIPLY_A
	
	MOVF	MULTIPLICADOR_DIVIDENDO,W 
	ADDWF	RESULTADO_LO,F
	BTFSC	STATUS,C
	INCF	RESULTADO_HI,F
	
	MOVF	MULTIPLICANDO_2,W
	ADDWF	RESULTADO_HI,F
		
	DECFSZ	MULTIPLICANDO_DIVISOR,F
	GOTO	MULTIPLY_A
	RETURN
	

;*	*	*	*	*	*	*	*	*	*	*	*
;*ROTINA DE CONVERSÃO DE BINÁRIO PARA DECIMAL*
;*	*	*	*	*	*	*	*	*	*	*	*

BIN_TO_DEC
	
	CLRF	CONVERSAO_0
	CLRF	CONVERSAO_1
	CLRF	CONVERSAO_2
	CLRF	CONVERSAO_3
	CLRF	CONVERSAO_4
		
	MOVF	LSB,F
	BTFSS	STATUS,Z
	GOTO	INC_MSB
	
	MOVF	MSB,F
	BTFSC	STATUS,Z
	RETURN
	
	MOVF	LSB,F
	BTFSC	STATUS,Z
	GOTO	CONVERSAO_A
	
INC_MSB	

	INCF	MSB,F
		
CONVERSAO_A

	;CLRWDT
	INCF	CONVERSAO_0,F
	MOVLW	.10
	XORWF	CONVERSAO_0,W
	BTFSS	STATUS,Z
	GOTO	CONVERSAO_B
	
	CLRF	CONVERSAO_0
	INCF	CONVERSAO_1,F
	MOVLW	.10
	XORWF	CONVERSAO_1,W
	BTFSS	STATUS,Z
	GOTO	CONVERSAO_B
	
	CLRF	CONVERSAO_1
	INCF	CONVERSAO_2,F
	MOVLW	.10
	XORWF	CONVERSAO_2,W
	BTFSS	STATUS,Z
	GOTO	CONVERSAO_B
	
	CLRF	CONVERSAO_2
	INCF	CONVERSAO_3,F
	MOVLW	.10
	XORWF	CONVERSAO_3,W
	BTFSS	STATUS,Z
	GOTO	CONVERSAO_B
	
	CLRF	CONVERSAO_3
	INCF	CONVERSAO_4,F
	
CONVERSAO_B	

	DECFSZ	LSB,F
	GOTO	CONVERSAO_A
	
	DECFSZ	MSB,F
	GOTO	CONVERSAO_A
		
	RETURN	
	
				
;*	*	*	*	*	*	*	*	*	*
;*          FIM DO PROGRAMA			*
;*	*	*	*	*	*	*	*	*	*


	END			; FIM DO PROGRAMA




